buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '[6.0,6.2)'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'

//    id 'net.minecraftforge.gradle'

    // This should be below the net.minecraftforge.gradle plugin
//    id 'org.parchmentmc.librarian.forgegradle' version '1.+'

    // Adds the Kotlin Gradle plugin
    id 'org.jetbrains.kotlin.jvm' version '1.9.22'

    // OPTIONAL Kotlin Serialization plugin
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.22'

    id 'com.gradleup.shadow' version '8.3.3'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = '1.1.3'
group = 'com.github.tartaricacid'
archivesBaseName = 'netmusic-1.20.1'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'parchment', version: '2023.08.20-1.20.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    copyIdeResources = true
    runs {
        client {
//            jvmArgs "-XX:+AllowEnhancedClassRedefinition"
            workingDirectory project.file('run/client_a')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'fml.earlyprogresswindow', 'false'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }

        client2 {
            parent minecraft.runs.client
            workingDirectory project.file('run/client_b')
            args '--username', 'tartaric_acid'
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }

        server {
//            jvmArgs "-XX:+AllowEnhancedClassRedefinition"
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'netmusic'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                netmusic {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        // location of the maven that hosts JEI files since January 2023
        // Patchouli
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        name = "Curse Maven"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
        content { includeGroup "thedarkcolour" }
    }
    maven {
        name = "IPN-Releases"
        url = "https://maven.ipn-mod.org/releases"
        mavenContent {
            releasesOnly()
        }
        content {
            includeGroup "org.anti_ad.mc"
            includeGroup "ca.solo-studios"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    minecraftLibrary 'com.googlecode.soundlibs:mp3spi:1.9.5.4'

    // compile against the JEI API but do not include it at runtime
    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"))

    compileOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge-api:${jei_version}"))
    // at runtime, use the full JEI jar for Forge
    runtimeOnly(fg.deobf("mezz.jei:jei-${minecraft_version}-forge:${jei_version}"))

    compileOnly "org.anti_ad.mc:InventoryProfilesNext-forge-1.20:1.10.7"

    compileOnly "org.anti_ad.mc:IPN-forge-1.20:1.10.9"

    // Adds KFF as dependency and Kotlin libs (use the variant matching your mod loader)
    // FORGE
    implementation 'thedarkcolour:kotlinforforge:4.10.0'

    implementation "com.squareup.okhttp3:okhttp:4.12.0"

    implementation "com.squareup.okio:okio:3.9.1"

    testImplementation "com.squareup.okhttp3:mockwebserver:4.12.0"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    dependencies {
        include(dependency("com.googlecode.soundlibs:mp3spi:1.9.5.4"))
        include(dependency("com.googlecode.soundlibs:jlayer:1.0.1.4"))
        include(dependency("com.googlecode.soundlibs:tritonus-share:0.3.7.4"))
        //include(dependency("com.squareup.okhttp3:okhttp:4.12.0"))

    }

    relocate 'javazoom.jl', 'com.github.tartaricacid.netmusic.soundlibs.javazoom.jl'
    relocate 'javazoom.spi', 'com.github.tartaricacid.netmusic.soundlibs.javazoom.spi'
    relocate 'org.tritonus', 'com.github.tartaricacid.netmusic.soundlibs.org.tritonus'
    //relocate 'okhttp3', 'com.github.tartaricacid.netmusic.dependencies.okhttp3'

    mergeServiceFiles()
}

reobf {
    shadowJar {}
}

tasks.register('shadowJarPublish', Jar) {
    dependsOn reobfShadowJar
    archiveClassifier = "all"
    from zipTree("${project.projectDir}/build/reobfShadowJar/output.jar")
}

assemble.dependsOn shadowJar
tasks.build.dependsOn shadowJarPublish
